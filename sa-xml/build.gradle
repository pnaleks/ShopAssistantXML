apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 26
    buildToolsVersion '27.0.3'
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 8
        versionName '1.0.7'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
    return void
}

def siteUrl = 'https://github.com/pnaleks/ShopAssistantXML'
def gitUrl = 'https://github.com/pnaleks/ShopAssistantXML.git'

bintray {
    user = properties.getProperty('bintray_user')
    key = properties.getProperty('bintray_key')

    version = android.defaultConfig.versionName
    //configurations = ['published', 'archives']
    configurations = ['archives']
    dryRun = false
    publish = true

    pkg {
        group = 'ru.pnapp'
        repo = 'pnapp'
        name = 'sa-xml'
        desc = 'XML parser for ShopAssistant project'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/pnaleks/ShopAssistantXML/issues'
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        //labels = ['aar', 'android', 'example']
        publicDownloadNumbers = true
        version {
            name = android.defaultConfig.versionName
            desc = 'Just a bit adjusted'
            vcsTag = android.defaultConfig.versionName
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'XML parser for ShopAssistant project'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'pnaleks'
                        name 'Petr Alekseev'
                        email 'pnaleks@jmail.com'
                    }
                }
                scm {
                    //connection 'https://github.com/bintray/bintray-examples.git'
                    //developerConnection 'https://github.com/bintray/bintray-examples.git'
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding = 'UTF-8'
    options.locale = 'ru'
    options.charSet = 'UTF-8'
    options.docEncoding = 'UTF-8'
    //options.links("http://simple.sourceforge.net/download/stream/doc/javadoc/")
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation('org.simpleframework:simple-xml:2.7.1') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    testImplementation 'junit:junit:4.12'
}

//
// Publishing: gradlew bintrayUpload
//